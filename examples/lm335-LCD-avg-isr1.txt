\ Homerseklet mérés LM335-el
\ Az utolsó 30 mérés átlagát jelzi ki LCD-n
\ Megszakításból fut, másodpercenként háromszor 

decimal

: array ( n -- ) ( i -- addr)
     create 4 * allot 
     does> swap 4 * + ;


30 constant ASIZE
14 constant LED
11 constant LM335
80000 constant MAXLOAD
variable cnt10ms
variable refresh
ASIZE array arr 


: avg-init ( n --- )
  ASIZE 0 do
    dup i arr !
  loop
  drop
;

: avg-shift
  ASIZE 1 - 0 do
    i 1 + arr @
    i arr !
  loop
;

: avg ( n --- avg )
  avg-shift
  ASIZE 1 - arr !
  0 arr @
  ASIZE 1 do
    i arr @ +
  loop
  ASIZE /
;

: ..lcd
  (..)
  lcd_on type lcd_off
;


: lm335@
  LM335 a@     ( read adc )
  3300 1024 */ ( convert adc to voltage )
  2730 -       ( convert kelvin to celsius )
;


: homero-isr
  1 cnt10ms +!
  cnt10ms @ refresh @ >= if
    0 cnt10ms !
    LED d@ 1 xor LED d!
    lcd_on
    ." Homerseklet:    "
    12 0 lcd_goto
    lm335@ avg
    1 ..
    0 1 lcd_goto
    load 1000 * MAXLOAD /
    ." Load:           " 
    5 1 lcd_goto
    2 .. ." %" 
    lcd_off
  then
;


: homero-isr-init
  1 LED pm
  0 cnt10ms !
  3 refresh !

  lm335@ avg-init
  1 0 255 1 2 3 6 7 255 255 255 255 lcd_init
  16 2 lcd_begin
  lcd_clear

  ['] homero-isr ['] isr_100ms defer!
;

: homero
  homero-isr-init
  ei
;

: autorun homero ;
syssave
  


